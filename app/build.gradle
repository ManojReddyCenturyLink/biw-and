apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'org.jetbrains.dokka'

apply plugin: 'kotlin-kapt'

apply plugin: "org.jlleitschuh.gradle.ktlint"

ext.localProperties = new Properties()
def propFile = rootProject.file('local.properties')
if (propFile.exists()) {
    localProperties.load(propFile.newDataInputStream())
}
ext.getLocalProperty = { propertyName ->
    def property = localProperties.getProperty(propertyName)
    return property == null ? "" : property
}

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.centurylink.biwf"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def subDomain = "qa"
        def rootUrl = "https://$subDomain-qa101.cs16.force.com/fiber"
        def baseUrl = "$rootUrl/services/data/v46.0/"
        def clientId = "3MVG9zJJ_hX_0bb9K7vLVk3OsI9xKsWw_HJHXUqrZt94ciDzSui3EjQhxwl6AXIDmKvZWU6_dhjfeUOghN7Wa"
        def scope = "web api refresh_token"
        def redirectUrl = "myapp://com.force.lightning.ctl-fiber--$subDomain"

        buildConfigField "String", "ROOT_URL", "\"$rootUrl\""
        buildConfigField "String", "BASE_URL", "\"$baseUrl\""
        buildConfigField "String", "CLIENT_ID", "\"$clientId\""
        buildConfigField "String", "SCOPE", "\"$scope\""
        buildConfigField "String", "REDIRECT_URL", "\"$redirectUrl\""

        manifestPlaceholders = [mapsApiKey: "AIzaSyARNegQ6g1SR7cHz_7LAQ7WbnP01nOi6YI"]
    }

    signingConfigs {
        debug {
            storeFile file("${project.rootDir}/debug.keystore")
            storePassword "password"
            keyAlias "debug"
            keyPassword "password"
        }

        release {
            def fileName = getLocalProperty("release_store_file")
            if (!fileName.isEmpty()) {
                storeFile file(fileName)
                storePassword getLocalProperty("release_store_password")
                keyAlias getLocalProperty("release_key_alias")
                keyPassword getLocalProperty("release_key_password")
            }
        }
    }

    buildTypes {
        release {
            def releaseMapsApiKey = getLocalProperty("release_maps_api_key")
            manifestPlaceholders = [mapsApiKey: "$releaseMapsApiKey"]

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    androidExtensions {
        experimental = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    //Android Core libraries and
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":library:mockintegrationserver")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "com.jakewharton.threetenabp:threetenabp:$threeThenABPVersion"

    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintLayoutVersion"
    implementation "androidx.viewpager2:viewpager2:${viewpager2Version}"
    implementation "androidx.recyclerview:recyclerview:${recyclerview}"
    implementation "androidx.cardview:cardview:$cardviewVersion"
    implementation "androidx.security:security-crypto:1.0.0-rc02"
    implementation "com.google.android.material:material:${materialComponentsVersion}"

    /* Dagger2 - default dependency */
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    //salesforce
    implementation "com.salesforce.service:chat-ui:4.1.1"

    /* Dagger2 - We are going to use dagger.android which includes
   * support for Activity and fragment injection so we need to include
   * the following dependencies */
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

    //Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    //LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidxLifecycleVersion"

    // Parceler in Kotlin
    implementation "org.parceler:parceler-api:$parcelerVersion"
    kapt "org.parceler:parceler:$parcelerVersion"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retroFit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retroFit2Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logginginterceptor"

    //timber
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.github.ome450901:SimpleRatingBar:$ratingVersion"

    // AppAuth
    implementation "net.openid:appauth:0.7.1"

    //biometrics
    implementation "androidx.biometric:biometric:$biometricVersion"

    //Google-map
    implementation "com.google.android.gms:play-services-maps:$googleMapVersion"

    //TEST IMPLEMENTATION
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "io.mockk:mockk:$mockKVersion"
    testImplementation "androidx.arch.core:core-testing:$archCoreTestingVersion"
    testImplementation "org.amshove.kluent:kluent-android:$kluentVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutineVersion"
    androidTestImplementation "androidx.test.ext:junit:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.4.0"
    testImplementation "org.threeten:threetenbp:1.4.4"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

ktlint {
    android = true
    outputToConsole = true
    outputColorName = "YELLOW"
    disabledRules = ["no-wildcard-imports", "import-ordering", "max-line-length"]
    filter {
        exclude("**/generated/**")
    }
}
